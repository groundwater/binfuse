#!/usr/bin/env coffee

{exec}  = require 'child_process'
program = require 'commander'
fs      = require 'fs'

program
.usage('[options] <name> <ami-id>')
.option('-r, --region   <KEY>', 'Select Region KEY')
.option('-L, --elb      <ELB>', 'Use Elastic Load Balancer')
.option('-E, --environ  <ENV>', 'Environment File')
.option('-T, --itype   <TYPE>', 'Instance Type')
.option('-m, --min        <N>', 'Minimum Instance')
.option('-M, --max        <N>', 'Maximum Instance')
.option('-Z, --zones  <ZONES>', 'List of Availability Zones')
.option('-S, --subnets <SUBS>', 'List of VPC Subnets')
.parse( process.argv )

if program.args.length < 2
  console.log "Please Specify a Group Name and Base AMI"
  process.exit -1

if fs.existsSync 'binfuse.json'
  options = JSON.parse fs.readFileSync 'binfuse.json'
else
  options = {}

defaults =
  zones: 'us-west-2a,us-west-2b,us-west-2c'
  region: 'us-west-2'
  min: 3
  max: 3
  itype: 'm1.small'

opt = (key)->
  options[key] || program[key] || defaults[key] || ''

AMI      = opt 'ami'
KEYPAIR  = opt 'keypair'
REGION   = opt 'region'

NAME   = program.args.shift()
AMI_ID = program.args.shift()

ENVIRONMENT   = opt 'environ'
INSTANCE_TYPE = opt 'itype'
LOAD_BALANCER = opt 'elb'
MIN_SIZE      = opt 'min'
MAX_SIZE      = opt 'max'
SUBNETS       = opt 'subnets'
ZONES         = opt 'zones'

WHEN = new Date().toISOString()
.replace(/-/g,'')
.replace('T','-')
.replace(/:/g,'')
.replace('.','')
.substr(2,14)

GROUPID = "#{NAME}@#{WHEN}"

config = "--launch-configuration #{GROUPID}"
region = "--region #{REGION}"
ami_id = "--image-id #{AMI_ID}"
type   = "--instance-type #{INSTANCE_TYPE}"
sizes  = "--min-size #{MIN_SIZE} --max-size #{MAX_SIZE}"

# Optional Settings
loadb  = if LOAD_BALANCER then "--load-balancers #{LOAD_BALANCER}"          else ""
health = if LOAD_BALANCER then "--health-check-type ELB --grace-period 300" else ""
data   = if ENVIRONMENT   then "--user-data-file #{ENVIRONMENT}"            else ""

# Options with Defaults
if SUBNETS
  azones = "--vpc-zone-identifier #{SUBNETS}"
else
  azones = "--availability-zones #{ZONES}"

## Create an Auto Scaling Group
# http://docs.aws.amazon.com/AutoScaling/latest/GettingStartedGuide/CreateASGroup.html

CREATE = "as-create-launch-config #{GROUPID} #{ami_id} #{type} #{region} #{data} 2>&1"
LAUNCH = "as-create-auto-scaling-group #{GROUPID} #{config} #{azones} #{sizes} #{region} #{loadb} #{health} 2>&1"

exec CREATE, (err,stdout,stderr)->
  exec LAUNCH, (err,stdout,stderr)->
    console.log GROUPID

# puts "Group deployment may take a few minutes"
# puts "Use the following command to view status"
# puts "as-describe-auto-scaling-instances --region us-west-2"

## Describe Auto Scaling Groups
# as-describe-auto-scaling-instances --region us-west-2

## Terminate All Instances in AS Group
# as-update-auto-scaling-group MyASGroup --min-size 0 --max-size 0 --region us-west-2
# as-delete-auto-scaling-group MyASGroup --region us-west-2
# as-delete-launch-config DemoGroup --region us-west-2

## Get Status from Load Balancer
# elb-describe-instance-health demo --region us-west-2
