#!/usr/bin/env coffee

colors  = require 'colors'
cp      = require 'child_process'
fs      = require 'fs'

program = require 'commander'

program
.usage('[options] <instance-id>')
.option('-r, --region   <KEY>', 'Select Region KEY')
.option('-I, --install <FILE>', 'Use Install File')
.parse( process.argv )

if program.args.length < 1
  program.help()

if fs.existsSync 'binfuse.json'
  options = JSON.parse fs.readFileSync 'binfuse.json'
else
  options = {}

defaults =
  region: 'us-west-2'
  install: "#{__dirname}/../data/install.bash"

opt = (key)->
  options[key] || program[key] || defaults[key] || ''

INSTALL = opt 'install'
REGION  = opt 'region'

instance_id = program.args.shift()

## End of Command Line Options ##

# Wait for the instance to boot
console.log "%s Waiting for Instance to be Ready", "[INFO]".cyan

instance_status  = "unknown"
instance_address = ""

install = ->
  
  SCRIPT = fs.readFileSync(INSTALL)
  LOGGER = fs.openSync 'out.log', 'a'
  
  try
    BUILDFILE = fs.readFileSync './Buildfile'
  catch e
    BUILDFILE = ''
    console.log '%s No Buildfile Found', '[INFO]'.cyan
  
  SSH = "ssh"
  SSH_ARGS = [ "-o", "StrictHostKeyChecking=no", "#{instance_address}"]
  GIT = "git"
  GIT_ARGS = ["push", "--verbose", "#{instance_address}:bundle/.git", "+master"]
  
  console.log "%s Logging Installation in File: out.log", "[INFO]".cyan
  console.log "%s Installing System Packages ", "[INFO]".cyan
  
  ssh = cp.spawn SSH, SSH_ARGS, {stdio: ['pipe',LOGGER,LOGGER] }
  ssh.stdin.write SCRIPT
  ssh.stdin.write BUILDFILE
  ssh.stdin.end()
  
  ssh.on 'exit', (code)->
    if code!=0
      console.log '%s System Failed to Install - check out.log for details', 'ERROR'.red
      return
    else
      console.log '  %s System Installed', '√'.green
    
    console.log "%s Installing Application ", '[INFO]'.cyan
    git = cp.spawn GIT, GIT_ARGS, {stdio: ['pipe','pipe','pipe']}
    git.stdout.on 'data', (data)-> fs.writeSync LOGGER, data, 0, data.length
    git.stderr.on 'data', (data)-> fs.writeSync LOGGER, data, 0, data.length
    git.on 'exit', (code)->
      if code != 0
        console.log '%s Application Failed to Install - check out.log for details', 'ERROR'.red
        return
      else
        console.log '  %s Application Installed', '√'.green

poll = ->
  
  describe_out = ""
  command      = "ec2-describe-instances #{instance_id} --region #{REGION}"
  
  cp.exec command, (err,stdout,stderr)->
    
    # handle errors
    
    describe_results = stdout.split /\s+/
    instance_status  = describe_results[9]
    instance_address = describe_results[7]
    
    if instance_status == "running"
      console.log "%s Instance Ready - Waiting for Boot", "[INFO]".cyan
      setTimeout install, 30000
    
    else
      setTimeout ->
        poll()
      , 3000

poll()

